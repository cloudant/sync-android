dependencies {
    compile project(':cloudant-sync-datastore-core')

    compile group: 'com.google.android', name: 'android', version:'4.0.1.2'

    // for unit tests
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.11'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile 'com.google.android:android-test:4.1.1.4'
    compile files('libs/android-support-v4.jar')
}

//
// Test: we don't want to run Android-specific tests on Java SE, but it's
// still useful to build them. So we just exclude all tests.
//

test {
     exclude '**/**'
}

//
// Publishing
//

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            pom.withXml {
                Node rootNode = asNode()

                rootNode.appendNode('name', 'Cloudant Sync Datastore Android: Android components')
                rootNode.appendNode('description', 'A JSON document datastore that syncs')
                rootNode.appendNode('url', 'https://cloudant.com/')
                rootNode.appendNode('packaging', 'jar')

                Node scmNode = rootNode.appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/cloudant/sync-android')
                scmNode.appendNode('connection', 'https://github.com/cloudant/sync-android.git')

                Node licencesNode = rootNode.appendNode('licenses')
                Node licenceNode = licencesNode.appendNode('license')
                licenceNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenceNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenceNode.appendNode('distribution', 'repo')
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}


