dependencies {
    compile project(':sync-core')

    compile group: 'com.almworks.sqlite4java', name: 'sqlite4java', version:'1.0.392'

    // there are a couple of unit tests in this project
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    // and we depend on some test utilities over in core
    testCompile project(':sync-core').sourceSets.test.output

}

tasks.withType(Test) {
    // the native dir is one directory up from the core project dir
    systemProperty "sqlite4java.library.path", "../native"
}

//
// Publishing
//

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            from components.java
            
            groupId 'com.cloudant'
            artifactId 'cloudant-sync-datastore-javase'
            
            pom.withXml {
                Node rootNode = asNode()
                
                rootNode.appendNode('name', 'Cloudant Sync Datastore Android: Java SE components')
                rootNode.appendNode('description', 'A JSON document datastore that syncs')
                rootNode.appendNode('url', 'https://cloudant.com/')
                rootNode.appendNode('packaging', 'jar')
                
                Node scmNode = rootNode.appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/cloudant/sync-android')
                scmNode.appendNode('connection', 'https://github.com/cloudant/sync-android.git')
                
                Node licencesNode = rootNode.appendNode('licenses')
                Node licenceNode = licencesNode.appendNode('license')
                licenceNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenceNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenceNode.appendNode('distribution', 'repo')
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}


