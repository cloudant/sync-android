// ************ //
// COMMON STUFF
// ************ //

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    //
    // Build plugins
    //

    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath "net.saliman:gradle-cobertura-plugin:2.1.0"
        }
    }

    //
    // General settings
    //

    group = 'com.cloudant'
    version = '0.3.6-SNAPSHOT'

    description = """cloudant-sync"""

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    defaultTasks 'build'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://cloudant.github.io/cloudant-sync-eap/repository/" }
    }

    //
    // Allow developer-local configuration to be applied (e.g., customising
    // location of maven repo).
    //

    def localGradleScript = file("../local.gradle")
    if (localGradleScript.exists()) {
        apply from: localGradleScript
    }

    //
    // Docs
    //

    javadoc {
        excludes = ["**/*sql*/**", "**/common/**", "**/android/**", "**/mazha/**"]
        options.showFromPublic()
        title = "cloudant-sync-datastore-android ${version} API"
    }

    //
    // Misc. tasks
    //

    task listJars << {
        configurations.testCompile.each { File file -> println file.name }
    }

}

// see http://issues.gradle.org/browse/GRADLE-1876
// run javadoc over union of all projects
task docs(type: Javadoc) {
    source subprojects.collect {project -> project.sourceSets.main.allJava } 
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath}) 
    destinationDir = new File(projectDir, 'docs')
}
